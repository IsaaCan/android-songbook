apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    signingConfigs {
        sign {
            keyAlias 'songbook-key'
            keyPassword 'songbook-key'
            storeFile file('/home/igrek/.android/songbook.jks')
            storePassword 'songbook-key'
        }
    }
    compileSdkVersion 27
    // automatic version incrementer
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        int vMajor = versionProps['VERSION_MAJOR'].toInteger()
        int vMinor = versionProps['VERSION_MINOR'].toInteger()
        int vPatch = versionProps['VERSION_PATCH'].toInteger() + 1
        int vCode = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_PATCH'] = vPatch.toString()
        versionProps['VERSION_CODE'] = vCode.toString()
        String vName = vMajor.toString() + "." + vMinor.toString() + "." + vPatch.toString()
        versionProps['VERSION_NAME'] = vName
        versionProps.store(versionPropsFile.newWriter(), null)
        defaultConfig {
            versionCode vCode
            versionName vName
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    defaultConfig {
        applicationId "igrek.songbook"
        minSdkVersion 16
        targetSdkVersion 27
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.sign
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    //implementation 'com.android.support:multidex:1.0.3'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'
    // Dagger 2
    implementation 'com.google.dagger:dagger:2.10'
    testImplementation 'com.google.dagger:dagger:2.10'
    androidTestImplementation 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // Guava
    implementation 'com.google.guava:guava:26.0-android'
    // RX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.0'
    // OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    // Unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.8'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    testImplementation 'org.assertj:assertj-core:3.10.0'
    // Android instumentation tests
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'org.mockito:mockito-core:2.22.0'
    androidTestImplementation 'org.mockito:mockito-android:2.22.0'
    androidTestImplementation 'org.assertj:assertj-core:3.10.0'
}
repositories {
    mavenCentral()
}
